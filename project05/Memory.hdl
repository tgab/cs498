// This file is part of the materials accompanying the book 
// "The Elements of Computing Systems" by Nisan and Schocken, 
// MIT Press. Book site: www.idc.ac.il/tecs
// File name: projects/05/Memory.hdl

/**
 * The complete address space of the computer's memory,
 * including RAM and memory mapped I/O. 
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: If load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory 
 * location specified by address. If load=1, the in value is loaded 
 * into the memory location specified by address.  This value becomes 
 * available through the out output starting from the next time step.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used. 
 * Access to address>0x6000 is invalid. Access to any address in 
 * the range 0x4000–0x5FFF results in accessing the screen memory 
 * map. Access to address 0x6000 results in accessing the keyboard 
 * memory map. The behavior in these addresses is described in the 
 * Screen and Keyboard chip specifications.
 */

CHIP Memory {

    IN  in[16], load, address[15];
    OUT out[16];

    PARTS:
    // Decide where to route input
    DMux4Way(in=load, sel=address[13..14], a=ram16a, b=ram16b, c=ram8, d=reg);
    Or(a=ram16a, b=ram16b, out=ram16);

    // RAM16K
    RAM16K(in=in, load=ram16, address=address[0..13], out=ram16out);
    
    // Screen memory map
    Screen(in=in, load=ram8, address=address[0..12], out=ram8out);

    // Keyboard
    Keyboard(out=regout);

    // Decide which output to use
    Mux4Way16(a=ram16out, b=ram16out, c=ram8out, d=regout, sel=address[13..14], out=out);
}
