// This file is part of the materials accompanying the book 
// "The Elements of Computing Systems" by Nisan and Schocken, 
// MIT Press. Book site: www.idc.ac.il/tecs
// File name: projects/03/1/RAM64.hdl

/**
 * Memory of 64 registers, each 16-bit wide.  
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = RAM64[address(t)](t)
 *     Write: If load(t-1) then RAM64[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory 
 * location specified by address. If load=1, the in value is loaded 
 * into the memory location specified by address.  This value becomes 
 * available through the out output starting from the next time step.
 */

CHIP RAM64 {

    IN in[16], load, address[6];
    OUT out[16];

    PARTS:
    //if load=1 choose which RAM8 to load into and pass in appropriate info for RAM8 to choose register
    DMux8Way(in=load, sel=address[3..5], a=a1, b=b1, c=c1, d=d1, e=e1, f=f1, g=g1, h=h1);
    RAM8(in=in, load=a1, address=address[0..2], out=at);
    RAM8(in=in, load=b1, address=address[0..2], out=bt);
    RAM8(in=in, load=c1, address=address[0..2], out=ct);
    RAM8(in=in, load=d1, address=address[0..2], out=dt);
    RAM8(in=in, load=e1, address=address[0..2], out=et);
    RAM8(in=in, load=f1, address=address[0..2], out=ft);
    RAM8(in=in, load=g1, address=address[0..2], out=gt);
    RAM8(in=in, load=h1, address=address[0..2], out=ht);

    //if load=0 choose which RAM8 to read from, RAM8 output already proper register
    Mux8Way16(a=at, b=bt, c=ct, d=dt, e=et, f=ft, g=gt, h=ht, sel=address[3..5], out=out);
}
